import sys

# Import variables from the parent build script
Import('env')

# Customize the construction environment
release = env.Clone()
release.Append(CXXFLAGS = '-O3')

# Build the engine library
release.StaticLibrary(target = release["ENGINE_LIBRARY"], 
                      source = release["ENGINE_SOURCES"])

# Build the console program
release.Program(target = release["CONSOLE_PROGRAM"], 
                source = release["CONSOLE_SOURCES"],
                LINKFLAGS = [ '-Wl' ],
                LIBS = release["LIBS"] + [ release["ENGINE_LIBRARY"] ])

srcfiles = release["GUI_SOURCES"]
if sys.platform == 'win32':
   srcfiles = srcfiles + release.RES(release['RESFILE'], release['RCFILE'])

# Build the GUI program
release.Program(target = release["GUI_PROGRAM"], 
                source = srcfiles,
                LIBS = release["LIBS"] + [ release["ENGINE_LIBRARY"] ])

