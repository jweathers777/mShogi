import sys

# Import variables from the parent build script
Import('env')

# Customize the construction environment
debug = env.Clone()
debug.Append(CXXFLAGS = '-g')
debug.Append(CPPDEFINES = 'DEBUG')

# Build the engine library
debug.StaticLibrary(target = debug["ENGINE_LIBRARY"], 
                    source = debug["ENGINE_SOURCES"])

# Build the console program
debug.Program(target = debug["CONSOLE_PROGRAM"], 
              source = debug["CONSOLE_SOURCES"],
              LINKFLAGS = [ '-Wl' ],
              LIBS = debug["LIBS"] + [ debug["ENGINE_LIBRARY"] ])

srcfiles = debug["GUI_SOURCES"]
if sys.platform == 'win32':
   srcfiles = srcfiles + debug.RES(debug['RESFILE'], debug['RCFILE'])

# Build the GUI program
debug.Program(target = debug["GUI_PROGRAM"], 
              source = srcfiles,
              LIBS = debug["LIBS"] + [ debug["ENGINE_LIBRARY"] ])

